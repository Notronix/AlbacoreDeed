apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'maven'
apply plugin: 'signing'

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco"
    }
}

findbugs {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
}

sourceCompatibility = 1.8
group = 'com.notronix'
version = '1.0.0'
sonatypeUsername = System.getenv("sonatype.username")
sonatypePassword = System.getenv("sonatype.password")

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.2'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.0'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                   'Implementation-Version': version,
                   'Implementation-Vendor': 'Notronix'
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name rootProject.name
                packaging 'jar'
                description rootProject.description
                url rootProject.url

                scm {
                    url 'scm:git@github.com:Notronix/' + rootProject.name + '.git'
                    connection 'scm:git@github.com:Notronix/' + rootProject.name + '.git'
                    developerConnection 'scm:git@github.com:Notronix/' + rootProject.name + '.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'clint.munden'
                        name 'Clint Munden'
                    }
                }
            }
        }
    }
}
